module modeling.Workflow

import org.eclipse.emf.mwe.utils.StandaloneSetup
import org.eclipse.xtend.typesystem.emf.EmfRegistryMetaModel
import org.eclipse.xtend.check.CheckComponent
import components.readers.*
import components.writers.*
import components.transformators.*

var sourceProjectName = "default.project" // has to be set by calling workflow
var generatedProjectBasePackage  = "de.unistuttgart.sample" // has to be set by calling workflow
var targetSimulatorProjectBaseName = "sample" // has to be set by calling workflow

var platformUriPrefix = "platform:/resource"
var moduleBaseName = "de.unistuttgart.iste.sqa.mpw"
var modelingModuleBaseName = "${moduleBaseName}.modeling"

var rootPath = "../.."
var transformationsProject = "${modelingModuleBaseName}.transformations"

var roleTransformationUri  = "${platformUriPrefix}/${transformationsProject}/RoleTransformation.qvto"
var commandTransformationUri  = "${platformUriPrefix}/${transformationsProject}/CommandTransformation.qvto"
var createWriteCommandTransformationUri = "${platformUriPrefix}/${transformationsProject}/CreateWriteCommandTransformation.qvto"
var queryTransformationUri  = "${platformUriPrefix}/${transformationsProject}/QueryTransformation.qvto"
var generationClassesTransformationUri  = "${platformUriPrefix}/${transformationsProject}/GenerationClassesTransformation.qvto"

Workflow {
	// set up EMF for standalone execution
	bean = StandaloneSetup { 
        scanClassPath = true
		platformUri=rootPath
		registerGeneratedEPackage="org.eclipse.emf.henshin.model.HenshinPackage"
		registerGeneratedEPackage="mpw.MpwPackage"
		registerGeneratedEPackage="commandBehavior.CommandPackage"
        registerGeneratedEPackage="queryBehavior.QueryPackage"
        registerGeneratedEPackage="behaviorInputs.InputsPackage"
        registerGeneratedEPackage="generationAnnotations.GenerationAnnotationsPackage"
	}
	
	// instantiate EMF meta-model
	bean = EmfRegistryMetaModel : mm_emf { }

	// find and read entity models
    component = MultiEcoreReader {
        rootPath = rootPath
        excludeModel = "stereotypes/*"
        projectName = sourceProjectName
        modelSlot = "entityModels"
    }

    // find and read Query-DSL input models
	component = QueryReader {
		rootPath = rootPath
		projectName = sourceProjectName
		modelSlot = "queries"
	}
	
    // find and read Henshin input models
	component = CommandReader {
		rootPath = rootPath
		projectName = sourceProjectName
		modelSlot = "commands"
	}
	
    // transform Query-DSL inputs to QueryBehavior models
	component = QvtoTransformator {
		transformationUri = queryTransformationUri
		modelSlot = "queriesComposition"
		targetModelSlot = "queries"
		sourceTargetRelationship = "NEW_TARGET"
	}
	
    // for debugging: write the "queries" into file under "/debugout"
    component = XmiWriter {
        rootPath = rootPath
        projectName = sourceProjectName
        modelSlot = "queries"
    }
	
    // transforms Henshin transformations to CommandBehavior models
	component = QvtoTransformator {
		transformationUri = commandTransformationUri
		modelSlot = "commands"
		sourceTargetRelationship = "NEW_TARGET"
	}
	
	// creates an implicit write command for the Actors found in the entityModels slot
	component = QvtoTransformator {
		transformationUri = createWriteCommandTransformationUri
        modelSlot = "entityModels"
        targetModelSlot = "commands"
		sourceTargetRelationship = "NEW_TARGET"
		mergeToExistingSlotContent = true
		errorOnTransformingNoOutput = false
	}
	
    // for debugging: write the "commands" into file under "/debugout"
	component = XmiWriter {
        rootPath = rootPath
        projectName = sourceProjectName
		modelSlot = "commands"
	}
	
    // transform "entityModels" to roles (Actor / Stage types will be splitted to ReadOnly/Editor/Game roles)
    component = QvtoTransformator {
		transformationUri = roleTransformationUri
		modelSlot = "entityModels"
	}
	
    // for debugging: write the "entityModels" before generation classes / facade transformation into file under "/debugout"
	/*component = XmiWriter {
        rootPath = rootPath
        projectName = sourceProjectName
		modelSlot = "entityModels"
		fileExtension = "ecore"
        directorySuffix = "TransformedNoFacades"
	}*/
    
    // transform "entityModels" with generation classes transformation (primarily adds facades)
    component = QvtoTransformator {
        transformationUri = generationClassesTransformationUri
        modelSlot = "entityModels"
        targetModelSlot = "generationEcoreModels"
    }
    
    // for debugging: write the "entityModels" finally before generation under "/debugout"
    component = XmiWriter {
        rootPath = rootPath
        projectName = sourceProjectName
        modelSlot = "generationEcoreModels"
        fileExtension = "ecore"
        directorySuffix = "Transformed"
    }
	
	// check model "entityModels"
	component = CheckComponent {
		metaModel = mm_emf
		checkFile = "util::Checks"
		emfAllChildrenSlot = "entityModels"
	}
	
	// check model "generationEcoreModels"
	component = CheckComponent {
		metaModel = mm_emf
		checkFile = "util::Checks"
		emfAllChildrenSlot = "generationEcoreModels"
	}
	
	// call Java generation
    component = @JavaGenerator auto-inject {
        rootPath = rootPath
        generatedProjectBasePackage = generatedProjectBasePackage
        targetSimulatorProject = "${targetSimulatorProjectBaseName}.java"
    }
    
    // call C++ generation
    component = @CppGenerator auto-inject {
        rootPath = rootPath
        generatedProjectBasePackage = generatedProjectBasePackage
        targetSimulatorProject = "${targetSimulatorProjectBaseName}.cpp"
    }
}
