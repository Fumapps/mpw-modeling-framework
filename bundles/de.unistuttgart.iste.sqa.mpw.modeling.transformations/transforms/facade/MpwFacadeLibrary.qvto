import facade.gamefacade.GameFacadeElementsBaseLibrary;
import util.BasicHelpers;

modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype CommandModel uses 'http://www.mpw.uni-stuttgart.de/behavior/command';
modeltype QueryModel uses 'http://www.mpw.uni-stuttgart.de/behavior/query';
modeltype GenerationAnnotations uses 'http://www.mpw.uni-stuttgart.de/generation/annotations';

/**
  *  Library used for the GenerationClassesTransformation which adds operations to the MPW class.
  *
  *  Most of operations are simple delegates to the underlying GameCommandStack.
  */
library StageBuilderLibrary;

mapping inout EClass::transformMpwClass()
    disjuncts EClass::transformConcreteMiniProgrammingWorldClass,
              EClass::transformMiniProgrammingWorldClass {};

mapping inout EClass::transformConcreteMiniProgrammingWorldClass()
    when { self.eAllSuperTypes->exists(t|t.name = "MiniProgrammingWorld") } {
    
    self->map createPerformanceDelegateOperation("startGame");
    self->map createPerformanceDelegateOperation("startGamePaused");
    self->map createPerformanceDelegateOperation("reset");
    self->map createPerformanceDelegateOperation("hardReset");
    self->map createPerformanceDelegateOperation("stopGame");
    self->map createPerformanceDelegateOperation("pauseGame");
    self->map createPerformanceDelegateOperation("resumeGame");
    self->map createConfirmAlertDelegateOperation();
    self->map createGameCommandStackGetter();

    self->map useFacades();
}

mapping inout EClass::transformMiniProgrammingWorldClass()
    when { self.name = "MiniProgrammingWorld" } {
    self.map removeDerivedStageReference();
}

abstract mapping inout EClass::createPerformanceDelegateOperation(operationName: String) {
    self.map createMemberDelegateOperation("performance", operationName);
}

abstract mapping inout EClass::createConfirmAlertDelegateOperation() {
    self.map createMemberDelegateOperation("userInputInterface", "confirmAlert");
}

abstract mapping inout EClass::createGameCommandStackGetter() {
    self.map createFeatureDelegate("performance", "gameCommandStack");
}

abstract mapping inout EClass::createMemberDelegateOperation(memberName: String, operationName: String) {
    var reference := eAllReferences->any(r|r.name = memberName);
    var operation := reference.eType.getOperationByName(operationName).deepclone();
    operation.eAnnotations += reference.createMemberVariableAnnotation();
    operation.eAnnotations += reference.createCallMethodAnnotation(operationName);
    eOperations += operation;
}

helper EReference::createCallMethodAnnotation(operationName: String) : MethodCallAnnotation {
    var operation := self.eType.getOperationByName(operationName);
    return object MethodCallAnnotation {
        variableName := self.name;
        methodName := operationName;
        closingStatement := true;
        parameterInputVariableExpressions := operation.eParameters->collect(p|createPathExpression(p));
    };
}

abstract mapping inout EClass::createFeatureDelegate(memberName: String, featureName: String) {
    var reference := eAllReferences->any(r|r.name = memberName);
    var feature := reference.eType.getFeatureByName(featureName);
    var getterOperation := object EOperation {
        name := "get" + featureName.firstToUpper();
        eType := feature.eType;
    };
    getterOperation.eAnnotations += reference.createMemberVariableAnnotation();
    getterOperation.eAnnotations += object ReturnAnnotation {
        expression := createPathExpression(reference, featureName);
    };
    eOperations += getterOperation;
}

helper EReference::createMemberVariableAnnotation() : VariableDefinitionAnnotation {
    return object VariableDefinitionAnnotation {
        variableName := self.name;
        variableTypeName := self.eType.name;
        rhs := object ExpressionAnnotation {
            expression := createStatementsExpression(object GetPropertyStatement {
                propertyGetterName := "get" + self.name.firstToUpper();
                type := self.eType;
            })
        };
    };
}

// MiniProgrammingWorld classes have to use facade classes
mapping inout EClass::useFacades() {
    eReferences->forEach(f) {
        if (f.eType.isMarkedWithAnnotation("role")) {
            var baseName := f.eType.getAnnotationValue("baseName");
            f.eType := findClassForName(baseName);
            f.eAnnotations += object InlineSimpleFieldInitializationAnnotation {
                field := f;
                parameterInputVariableExpressions += createSelfExpression("this", self);
            };
        }
    };
}

mapping inout EClass::removeDerivedStageReference() {
    self.eStructuralFeatures := self.eStructuralFeatures->reject(f|f._derived and f.eType.name = "Stage");
}
