modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');

/**
  *  Library used to have simpler convinience methods to deal with EAnnotations.
  */
library AnnotationHelpers;

helper String::toMarkerAnnotation() : EAnnotation {
    return object EAnnotation {
        source := self;
    };
}

query EModelElement::isMarkedWithAnnotation(name: String) : Boolean {
    return self.eAnnotations->exists(a|a.source = name);
}

helper toAnnotation(key: String, value: String) : EAnnotation {
    return object EAnnotation {
        source := key;
        var entry := object EStringToStringMapEntry {};
        entry.key := key;
        entry.value := value;
        details += entry;
    };
}

query EModelElement::getAnnotationValue(source: String) : String {
    var annotation := self.eAnnotations->any(a|a.source = source);
    if (annotation != null) {
    	return annotation.details->first().value;
    };
    return null;
}

query EModelElement::getAnnotationValueIgnoringSource(key: String) : String {
    var annotation := self.eAnnotations->any(a|a.details->exists(d|d.key = key));
    if (annotation != null) {
    	return annotation.details->any(d|d.key = key).value;
    };
    return null;
}

query EModelElement::hasAnnotationValue(key: String, value: String) : Boolean {
    return self.getAnnotationValue(key) = value;
}
