import ecore;

extension util::Extensions reexport;
extension template::util::EmfGeneratorExtensions reexport;

operationToSignature(EOperation this) :
    let parameters = if (isCommand()) then "CommandStack commandStack" else eParameters.collect(e|e.eType.name.toJavaType() + " " + e.name).joinToString(", ") :
    operationToJavaType() + " " + effectiveOperationName() + "(" + parameters + ")";

private effectiveOperationName(EOperation this) : 
    if (isPropertyGetter()) then name.getter() else name;

hasPropertyGetterOperation(EStructuralFeature this) :
    let class = (EClass)eContainer :
    class.eAllSuperTypes.exists(t|t.eOperations.exists(o|o.isPropertyGetter() && o.name == name));
    
isPropertyGetter(EOperation this) : isMarkedWith("isPropertyGetter");
    
isCommand(EOperation this) : isMarkedWith("isCommand");
    
isQuery(EOperation this) : isMarkedWith("isQuery");

private isMarkedWith(EOperation this, String key) :
    eAnnotations.exists(a|a.details.exists(d|d.key == key));