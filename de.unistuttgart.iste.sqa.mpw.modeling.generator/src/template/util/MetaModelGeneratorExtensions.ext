import ecore;

extension util::Extensions reexport;
extension template::operations::BuiltInOperationsExtensions reexport;
extension template::util::GlobalsGeneratorExtensions reexport;
extension template::util::OperationGeneratorExtensions reexport;

isStereotype(EClassifier this) :
    EClass.isInstance(this) && ((EClass)this).eSuperTypes.exists(t|t.name == "ViewStereotype");

needsImplSubclass(EClass this) :
	!isViewModelType() && getManualImplementedOperations().size > 0 && !abstract;

getManualImplementedOperations(EClass this) :
    getUserDefinedOperations().reject(o|o.isGenerated());

private isGenerated(EOperation this) :
    isCommand() || isQuery() || isPropertyGetter();

isViewModelType(EClassifier this) :
    EClass.isInstance(this) && ((EClass)this).eSuperTypes.exists(t|t.name == "ViewModel");

toInheritance(EClass this) :
    let filteredExtends = eSuperTypes.typeSelect(EClass)
    	.reject(e|e.interface) :
    let filteredInterfaces = eSuperTypes.typeSelect(EClass)
        .reject(e|e.isStereotype())
    	.select(e|e.interface) :
    if (filteredExtends.size > 0 && filteredInterfaces.size > 0) then "extends " + filteredExtends.get(0).name + " implements " + filteredInterfaces.collect(i|i.name).toString(", ")
    else if (filteredExtends.size > 0) then "extends " + filteredExtends.get(0).name
    else if (interface && filteredInterfaces.size > 0) then "extends " + filteredInterfaces.collect(i|i.name).toString(", ")
    else if (filteredInterfaces.size > 0) then "implements " + filteredInterfaces.collect(i|i.name).toString(", ")
    else "";

toJavaPackagePath(EPackage this) :
    this.toJavaPackageName().replaceAll("\\.", "/");

toJavaPackageName(EPackage this) :
    getModulePackage() + "." + this.name;
