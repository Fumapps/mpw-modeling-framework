import facade.FacadeElementsLibrary;

import helpers.AnnotationHelpers;
import helpers.EmfHelpers;

modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');

modeltype CommandModel uses 'http://www.mpw.uni-stuttgart.de/behavior/command';
modeltype QueryModel uses 'http://www.mpw.uni-stuttgart.de/behavior/query';
modeltype GenerationAnnotations uses 'http://www.mpw.uni-stuttgart.de/generation/annotations';

transformation GenerationClassesTransformation(in inEcore: Ecore, out outEcore: Ecore);

configuration property Queries: List(QueryModel::Module);
configuration property Commands: List(CommandModel::Transformation);
configuration property EntityModels: List(CommandModel::Transformation);

main() {
    inEcore.rootObjects()[EPackage]->map transformPackage();
}

mapping EPackage::transformPackage() : EPackage {
    name := self.name;
    nsURI := self.nsURI + "/generation";
    nsPrefix := self.nsPrefix;
    // eClassifiers := self.eClassifiers->map toClassifier();
    // eClassifiers += self.eClassifiers[EClass]->map toImplClass();
    
    addEntityModelToGlobalSearchList(result);
    
    var facadeClasses := self.eClassifiers[EClass][hasAnnotationValue("role", "game")]->map toGameFacadeClass();
    eClassifiers += facadeClasses;
    
    // generate elements of facade after classes are created, since they may rely on other facade types
    facadeClasses->map createElements();
    
    self.eClassifiers[EClass][isInstanceOf("MiniProgrammingWorld")]->map useFacades();
}

mapping EClassifier::toClassifier() : EClassifier
    disjuncts EClass::toClassifier,
              EEnum::toClassifier {}

mapping EClass::toClassifier() : EClass {
    name := self.name;
}

mapping EEnum::toClassifier() : EEnum {
    name := self.name;
}

mapping EClass::toImplClass() : EClass {
    name := self.name + "Impl";
}

mapping EClass::toGameFacadeClass() : EClass {
    var baseName := self.getAnnotationValue("baseName");
    name := baseName;
}

// MiniProgrammingWorld classes have to use facade classes
mapping inout EClass::useFacades() {
    eReferences->forEach(f) {
    	if (f.eType.isMarkedWithAnnotation("role")) {
    	    var baseName := f.eType.getAnnotationValue("baseName");
    		f.eType := findClassForName(baseName);
    		f.eAnnotations += object FacadeElementFieldInitialization {
    			field := f;
    		};
    		f.eAnnotations += object EAnnotation {
    			source := "fieldInitialization";
    			var code := "new " + f.eType.name + "(this)";
    			contents += code.oclAsType(EObject);
    		};
    	}
    };
}

