import helpers.ExpressionTransformation;

modeltype ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');
	
modeltype Input uses "http://www.mpw.uni-stuttgart.de/transformation/inputs";
modeltype QueryInput uses "http://www.mpw.uni-stuttgart.de/querydsl";
modeltype QueryModel uses "http://www.mpw.uni-stuttgart.de/behavior/query";

transformation TransformQueryDsl2Query(in inModel: QueryInput, out outModel: QueryModel);

configuration property EntityModels: List(EPackage);

main() {
    inModel.rootObjects()[Input::QueryInputs]->map toQueryModel();
}

mapping Input::QueryInputs::toQueryModel() : QueryModel::Module {
    name := "queries";
    elements := self.models->map toElement();
}

mapping QueryInput::Model::toElement() : QueryModel::ExpressionalElement
    disjuncts QueryInput::Model::toQuery, QueryInput::Model::toPrecondition, QueryInput::Model::toPostcondition { }

abstract mapping QueryInput::Model::toElementBase() : QueryModel::ExpressionalElement {
    init {
        result.name := self.context.name;
        result.contextClass := self.context.className.map toContextClass();
        result.mainExpression := self.expressions->first().map toExpression();
    }
}

mapping QueryInput::Model::toQuery() : QueryModel::Query inherits QueryInput::Model::toElementBase
	when { self.context.kind = ContextKind::QUERY; } {
	
	if (result.mainExpression.oclIsTypeOf(QueryModel::StatementsExpression)) {
		var statementExpression := result.mainExpression.oclAsType(QueryModel::StatementsExpression);
		result.eType := statementExpression.statements->last().eType;
	} else {
		result.eType := ecore::EBoolean.oclAsType(ecore::EClassifier);
	};
	defaultValueLiteral := result.eType.defaultValue.toString();
}

mapping QueryInput::Model::toPrecondition() : QueryModel::Precondition inherits QueryInput::Model::toElementBase
    when { self.context.kind = ContextKind::PRE; } {
    commandName := self.context.name;
}

mapping QueryInput::Model::toPostcondition() : QueryModel::Postcondition inherits QueryInput::Model::toElementBase
    when { self.context.kind = ContextKind::POST; } {
    commandName := self.context.name;
}

mapping String::toContextClass() : ecore::EClass {
	init { result := findClassifierForName(self).oclAsType(ecore::EClass); }
}
