modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype Commandgt uses commandgt('http://www.mpw.uni-stuttgart.de/commandgt');

transformation RoleTransformation(inout ecore: Ecore);

configuration property Queries: List(String);
configuration property Commands: List(Commandgt::Transformation);

main() {
  ecore.rootObjects()[EPackage]->map transformClasses();
}

mapping Ecore::EPackage::transformClasses() {
  var classes := self.eClassifiers[EClass];
  classes[isRelevantType()]->map transformToRoles();
}

helper Ecore::EClass::isRelevantType() : Boolean {
  return self.eAllSuperTypes->exists(t|t.name = "Actor" or t.name = "Stage");
}

mapping inout Ecore::EClass::transformToRoles() {
  var baseName := self.name;
  self.name := "Concrete" + baseName;
  
  var readOnlyRole := createReadOnlyRole(baseName);
  
  var gameRole := self.createCommandRole(baseName, "game");
  gameRole.eSuperTypes += readOnlyRole;
  self.eSuperTypes += gameRole;
  
  var editorRole := self.createCommandRole(baseName, "editor");
  editorRole.eSuperTypes += readOnlyRole;
  self.eSuperTypes += editorRole;
}

helper Ecore::EClass::createReadOnlyRole(baseName: String) : Ecore::EClass {
  var readOnlyRole := object EClass {
  	name := "ReadOnly" + baseName;
  	_abstract := true;
  	interface := true;
  };
  readOnlyRole.eOperations += self.eAllStructuralFeatures->select(f|f.changeable)->map transformFeatureToGetterOperation();
  readOnlyRole.eOperations += Queries->select(q|q.startsWith(baseName))->map transformQueryToOperation();
  self.ePackage.eClassifiers += readOnlyRole;
  return readOnlyRole;
}

helper Ecore::EClass::createCommandRole(baseName: String, roleName: String) : Ecore::EClass {
  var roleClass := object EClass {
  	name := roleName.firstToUpper() + baseName;
  	_abstract := true;
  	interface := true;
  };
  roleClass.eOperations += Commands->select(c|c.roleName = roleName and c.targetClassName = baseName)->map transformCommandToOperation();
  self.ePackage.eClassifiers += roleClass;
  return roleClass;
}

mapping String::transformQueryToOperation() : EOperation {
	name := self.substringAfter("|");
	eType := EBoolean.oclAsType(EClassifier);
	eAnnotations += "isQuery".toMarkerAnnotation();
}

mapping EStructuralFeature::transformFeatureToGetterOperation() : EOperation {
	name := self.name;
	eType := self.eType;
	upperBound := self.upperBound;
	eAnnotations += "isPropertyGetter".toMarkerAnnotation();
}

mapping Commandgt::Transformation::transformCommandToOperation() : EOperation {
	name := self.name;
	eAnnotations += "isCommand".toMarkerAnnotation();
}

helper String::toMarkerAnnotation() : EAnnotation {
	return object EAnnotation {
	  details += object EStringToStringMapEntry {
	  	  key := self;
	  };
	};
}

