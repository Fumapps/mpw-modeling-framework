import ecore;
import generationAnnotations;

extension template::operations::OperationGeneratorExtensions;
extension template::properties::PropertyGeneratorExtensions;
extension template::types::MetaModelGeneratorExtensions;

/*
 * Extensions related to EMF Metamodel types.
 */

getter(String this) :
	"get" + this.toFirstUpper();
	
setter(String this) :
	"set" + this.toFirstUpper();
	
adder(String this) :
    "addTo" + this.toFirstUpper();
    
remover(String this) :
    "removeFrom" + this.toFirstUpper();

adderOrSetter(EStructuralFeature this) :
    if (isCollection()) then name.adder() else name.setter();

modifierParameterName(EStructuralFeature this) :
    if (isCollection()) then name.toSingleName() else name;

previousFieldName(EStructuralFeature this) :
    "previous" + modifierParameterName().toFirstUpper();
    
modifierVisibility(EStructuralFeature this) :
    if (derived) then "protected" else "public";
    
private toSingleName(String this) : 
    if (endsWith("ies")) then subString(0, length-3) + "y"
    else if (endsWith("s")) then subString(0, length-1)
    else this;
	
isCollection(ETypedElement this) :
    upperBound == -1;
    
isReference(EStructuralFeature this) :
    EReference.isInstance(this);
	
isConstructorInjectedProperty(EStructuralFeature this) :
    !this.changeable && !this.isContainment() && !this.derived;

isDerivedFrom(EClassifier this, String superTypeName) :
    let eClass = (EClass)this :
    EClass.isInstance(this) && eClass.eAllSuperTypes.exists(t|t.name == superTypeName);
    
isContainment(EStructuralFeature this) :
    EReference.isInstance(this) && ((EReference)this).containment;
    
toJavaType(String this) :
	if (this == "EInt") then "int"
	else if (this == "EBoolean") then "boolean"
	else if (this == "EDouble") then "double"
	else if (this == "EString") then "String"
	else if (this == "EObject") then "Object"
	else this;
	
isBuiltInType(EClassifier this) :
    isPrimitive() || this.name == "Object" || isSemaphore() || this.name == "Throwable";

isPrimitive(EClassifier this) :
    (this.name == "EInt" || this.name == "EBoolean" || this.name == "EDouble" || this.name == "EString");

featureToSingleJavaType(EStructuralFeature this) : this.eType.name.toJavaType();
featureToJavaType(EStructuralFeature this) : elementToJavaType();
operationToJavaType(EOperation this) : elementToJavaType();
	
private elementToJavaType(ETypedElement this) :
    if (isMarkedWithAnnotation("constructor")) then "" 
    else if (this.eType == null) then "void" 
	else if (this.upperBound == -1) then "List<" + this.eType.name.toJavaType() + ">"
	else this.eType.name.toJavaType();

hasInitializationAnnotation(EStructuralFeature this) :
    getInitializationAnnotation() != null;

getInitializationAnnotation(EStructuralFeature this) :
    this.eAnnotations.typeSelect(InlineSimpleFieldInitializationAnnotation).first();

featureToFieldInstantiation(EStructuralFeature this) :
    if (hasInitializationAnnotation()) then "ERROR: featureToFieldInstantiation() must not be called for InlineSimpleFieldInitializationAnnotation"
	else if (this.upperBound == -1) then "new ArrayList<>()"
	else if (this.eType.isDependency()) then "new " + this.eType.ePackage.toJavaPackageName() + ".impl.Dummy" + this.eType.name + "()"
	else if (this.eType.needsImplSubclass()) then "new " + this.eType.ePackage.toJavaPackageName() + ".impl." + this.eType.name + "Impl()"
	else "new " + this.eType.name.toUnboxedJavaTypeName() + "()";
	
toUnboxedJavaTypeName(String this) :
	if (this == "EInt") then "Integer"
	else if (this == "EBoolean") then "Boolean"
	else if (this == "EDouble") then "Double"
	else if (this == "EString") then "String"
	else if (this == "EObject") then "Object"
	else this;
	
defaultValueOf(EStructuralFeature f) :
    if (f.defaultValueLiteral != null && f.eType.isEnum()) then f.eType.name + "." + f.defaultValueLiteral
    else if (f.defaultValueLiteral != null) then f.defaultValueLiteral
    else if (f.eType.name == "EInt") then "0"
	else if (f.eType.name == "EBoolean") then "false"
	else if (f.eType.name == "EDouble") then "0.0"
	else null;
    
List[EClassifier] getUsedTypes(EOperation this) :
    this.eParameters.collect(p|p.eType).union({this.eType}.reject(r|r == null));
    
toJavaDoc(String this) :
    if (length > 0) then 
        "/** " + this + " */"
    else "";
    
toComment(String this) :
    if (length > 0) then 
        "/* " + this + " */"
    else "";

isMarkedWithAnnotation(EModelElement this, String key) : 
    eAnnotations.exists(a|a.source == key);

getAnnotationValue(EModelElement this, String key) : 
    eAnnotations.selectFirst(a|a.source == key).details.selectFirst(d|d.key == key).value;
    
isEnum(EClassifier this) : EEnum.isInstance(this);
